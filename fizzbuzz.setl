#!/usr/bin/env setl

-- Language:       SETL
-- Web site:       https://setl.org/setl/
-- Last tested on: Ubuntu 24.04.2 LTS
-- Requires:       Install precompiled executables from
--                 https://setl.org/setl/bin/Linux-x86-64bit/

program Fizz_Buzz;
    fizz := {3, 6..100};
    buzz := {5, 10..100};
    fizzbuzz := fizz * buzz;
    fizz -:= fizzbuzz;
    buzz -:= fizzbuzz;
    num := {1..100} - fizz - buzz - fizzbuzz;
    line_map := {[n, "Fizz"]     : n in fizz} +
                {[n, "Buzz"]     : n in buzz} +
                {[n, "FizzBuzz"] : n in fizzbuzz} +
                {[n, n]          : n in num};
    loop for i in [1..100] do
        print(line_map(i));
    end loop;
end Fizz_Buzz;
